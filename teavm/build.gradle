plugins {
  id 'java'
  id 'org.gretty' version '3.1.0'
}

gretty {
  contextPath = '/'
  extraResourceBase 'build/dist/webapp'
}

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
project.ext.mainClassName = 'lando.systems.ld53.teavm.TeaVMBuilder'
eclipse.project.name = appName + '-teavm'

dependencies {
  implementation "com.github.xpenatan.gdx-teavm:backend-teavm:$gdxTeaVMVersion"
  implementation "com.github.xpenatan.gdx-teavm:backend-web:$gdxTeaVMVersion"
  implementation "com.github.xpenatan.gdx-teavm:gdx-freetype-teavm:$gdxTeaVMVersion"
  implementation project(':core')

}

task buildJavaScript(dependsOn: classes, type: JavaExec) {
  setDescription("Transpile bytecode to JavaScript via TeaVM")
  mainClass.set(project.mainClassName)
  setClasspath(sourceSets.main.runtimeClasspath)
}
build.dependsOn buildJavaScript

task run(dependsOn: [buildJavaScript, ":teavm:jettyRun"]) {
  setDescription("Run the JavaScript application hosted via a local Jetty server at http://localhost:8080/")
}
